plugins {
    id 'java'
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

tasks.build.dependsOn('shadowJar')

group = 'com.nickuc.report'
version = '3.0.2'

repositories {
    mavenCentral()
    maven { url = uri('https://papermc.io/repo/repository/maven-public/') }
    maven { url = uri('https://nexus.velocitypowered.com/repository/maven-public/') }
    maven { url = uri('https://libraries.minecraft.net/') }
}

dependencies {
    def mojangAuthLib = '1.5.21'
    def paperVersion = '1.15.2-R0.1-SNAPSHOT'
    def waterfallVersion = '1.18-R0.1-SNAPSHOT'
    compileOnly "com.mojang:authlib:${mojangAuthLib}"
    compileOnly "com.destroystokyo.paper:paper-api:${paperVersion}"
    compileOnly "io.github.waterfallmc:waterfall-api:${waterfallVersion}"

    def velocityVersion = '3.0.1'
    compileOnly "com.velocitypowered:velocity-api:${velocityVersion}"
    annotationProcessor "com.velocitypowered:velocity-api:${velocityVersion}"

    def lombok = "org.projectlombok:lombok:1.18.24"
    compileOnly lombok
    annotationProcessor lombok
    testCompileOnly lombok
    testAnnotationProcessor lombok

    def bstatsVersion = '3.0.0'
    implementation "org.bstats:bstats-bukkit:${bstatsVersion}"
    implementation "org.bstats:bstats-bungeecord:${bstatsVersion}"
    implementation "org.bstats:bstats-velocity:${bstatsVersion}"
}

blossom {
    replaceTokenIn('src/main/java/com/nickuc/report/bootstrap/nReportVelocity.java')
    replaceToken '@version', project.version
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': version
        include 'plugin.yml'
        include 'bungee.yml'

        duplicatesStrategy = 'include'
    }
}

shadowJar {
    archiveName('nReport.jar')

    relocate 'org.bstats', project.group + '.metrics'
}